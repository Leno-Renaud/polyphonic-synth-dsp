import("stdfaust.lib");

// =======================
// PARAMÈTRES
// =======================

freq   = hslider("freq", 440, 27.5, 4186, 0.01);
gain   = hslider("volume", 0.5, 0, 1, 0.001);
strike = button("strike");
mode   = hslider("mode", 0, 0, 11, 1);  

// =======================
// OPTIONS SYNTHÉ
// =======================

// ADSR
attack  = hslider("ADSR/Attack", 0.005, 0.001, 2, 0.001);
decay   = hslider("ADSR/Decay", 0.08, 0.001, 2, 0.001);
sustain = hslider("ADSR/Sustain", 0.3, 0, 1, 0.01);
release = hslider("ADSR/Release", 0.2, 0.001, 3, 0.001);

// Vibrato
vibRate  = hslider("Modulation/VibratoRate", 5, 0.1, 20, 0.1);
vibDepth = hslider("Modulation/VibratoDepth", 0.003, 0, 0.02, 0.0001);

// Filtres
lowCut  = hslider("Filter/Lowpass", 10000, 200, 20000, 1);
highCut = hslider("Filter/Highpass", 20, 20, 2000, 1);

// Pan
pan = hslider("Spatial/Pan", 0.5, 0, 1, 0.01);

// =====================
// ENVELOPPE
// =======================
// strike enter adsr (if 1 dcp start, si 0 on release)
env = strike : en.adsr(attack, decay, sustain, release);

// =======================
// VIBRATO
// =======================
//osc lente pour near 1
vibrato = freq * (1 + vibDepth * os.osc(vibRate));

// =======================
// MODES
// =======================

supersaw = ( os.sawtooth(vibrato)*0.5 + os.sawtooth(vibrato*1.01)*0.25 + os.sawtooth(vibrato*0.99)*0.25 ) * env;

celestial = ( os.osc(vibrato)*0.4 + os.osc(vibrato*2.0)*0.3 + os.triangle(vibrato*4.0)*0.2 + os.osc(vibrato*8.0)*0.1 ) * env;

crystal = ( os.osc(freq + os.osc(freq*4.0)*(freq*0.5)) ) * env;

bell = ( os.osc(freq)*0.8 + os.osc(freq*2.7)*0.35 + os.osc(freq*5.1)*0.20 ) * env;

warmPad = ( os.triangle(vibrato)*0.6 + os.osc(vibrato*0.5)*0.3 + os.osc(vibrato*2.0)*0.1 ) * env;

brightPluck = ( os.pulsetrain(freq,0.3)*0.7 + os.sawtooth(freq*2.0)*0.3 ) * env;

epiano = ( os.osc(freq) + os.osc(freq*2.0)*0.4 + os.osc(freq*3.0)*0.2 ) * env;

softStrings = ( os.sawtooth(vibrato)*0.5 + os.sawtooth(vibrato*1.01)*0.3 + os.osc(vibrato*0.5)*0.2 ) * env;

spaceBass = ( os.square(freq*0.5)*0.7 + os.osc(freq*1.01)*0.3 ) * env;

glass = ( os.osc(freq) + os.osc(freq*2.5)*0.5 + os.osc(freq*4.0)*0.3 ) * env;

dreamChoir = ( os.osc(vibrato)*0.6 + os.osc(vibrato*1.02)*0.4 ) * env; 

richOrgan = ( os.osc(freq) + os.osc(freq*2.0)*0.5 + os.osc(freq*4.0)*0.3 ) * env;


// =======================
// SELECTEUR 0–19
// =======================

synth =
  (mode == 0)*supersaw +
  (mode == 1)*celestial +
  (mode == 2)*crystal +
  (mode == 3)*bell +
  (mode == 4)*warmPad +
  (mode == 5)*brightPluck +
  (mode == 6)*epiano +
  (mode == 7)*softStrings +
  (mode == 8)*spaceBass +
  (mode == 9)*glass +
  (mode == 10)*dreamChoir +
  (mode == 11)*richOrgan;


// =======================
// CHAÎNE EFFETS
// =======================

filtered = synth : fi.lowpass(2, lowCut) : fi.highpass(1, highCut);

mix = filtered;

left  = mix * gain * (1 - pan);
right = mix * gain * pan;

process = left, right;